buildscript {
    repositories {
        maven {
            url 'http://nexus.intr/repository/mavengroup'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 5, 'minutes'
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("ru.majordomo.hms.rc.staff:resources:1+")
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
    id "com.gorylenko.gradle-git-properties" version "2.1.0"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: 'io.spring.dependency-management'

def versionPropsFile = file('version.properties')
def versionBuild

if (versionPropsFile.canRead()) {
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    versionBuild = versionProps['VERSION_BUILD'].toInteger()
} else {
    throw new GradleException("Could not read version.properties!")
}

ext.autoIncrementBuildNumber = {
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'ru.majordomo.hms.rc.staff'
            artifactId 'resources'
            version '2.0.' + versionBuild
            artifact resourceJar
        }
    }
    repositories {
        maven {
            url uri(mavenUrl)
            credentials {
                username = mavenUser
                password = mavenPass
            }
        }
    }
}

version = '0.0.1-SNAPSHOT'

jar {
    baseName = 'rc-staff'
    version = version
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

springBoot {
    buildInfo()
}

task resourceJar(type: Jar, dependsOn: compileJava) {
    baseName = 'resources'
    from sourceSets.main.output.classesDir
    include 'ru/majordomo/hms/rc/staff/resources/**'
}

repositories {
    maven {
        url 'http://nexus.intr/repository/mavengroup'
    }
    maven { url 'http://nexus.intr/repository/spring_milestone' }
}

dependencies {
    implementation('ru.majordomo.hms.rc.staff:resources:1+')
    implementation('ru.majordomo.hms.personmgr:exceptions:1.0.17')
    implementation('org.springframework.cloud:spring-cloud-starter-config')
    implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
    implementation 'org.springframework.cloud:spring-cloud-starter-security'
    implementation('org.springframework.cloud:spring-cloud-starter-bus-amqp')
    implementation('org.springframework.cloud:spring-cloud-starter-oauth2')
    implementation("org.springframework.cloud:spring-cloud-starter-sleuth")
    implementation('org.springframework.cloud:spring-cloud-starter-openfeign')
    implementation('org.springframework.boot:spring-boot-starter-data-mongodb')
    implementation('org.springframework.boot:spring-boot-starter-amqp')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-jdbc')
    implementation('commons-net:commons-net:3.5')
    implementation("org.projectlombok:lombok:1.18.2")
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    implementation(group: 'com.mysql', name: 'management', version: '1.1.6') {
        exclude(module: 'jmxtools')
    }
//    testCompile('com.github.fakemongo:fongo:2.2.0-RC2')
    testCompile('de.bwaldvogel:mongo-java-server:1.19.0')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc:${springRestDocsVersion}")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springVersion}"
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir 'src/main/asciidoc'
}

build.dependsOn("resourceJar")
publish.doFirst autoIncrementBuildNumber
